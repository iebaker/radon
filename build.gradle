import org.gradle.internal.os.OperatingSystem

switch (OperatingSystem.current()) {
	case OperatingSystem.WINDOWS:
		project.ext.lwjglNatives = "natives-windows"
		break
	case OperatingSystem.LINUX:
		project.ext.lwjglNatives = "natives-linux"
		break
	case OperatingSystem.MAC_OS:
		project.ext.lwjglNatives = "natives-macos"
		break
}

project.ext.lwjglVersion = "3.1.0"
project.ext.jomlVersion = "1.8.2"
project.ext.jsonVersion = "20160810"
project.ext.junitVersion = "4.12"
project.ext.jbulletVersion = "20101010-1"

buildscript {
	repositories {
		jcenter()
	}

	dependencies {
		classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
	}
}

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    mavenCentral()
}

dependencies {
   	compile "org.lwjgl:lwjgl-opengl:${lwjglVersion}"
	compile "org.lwjgl:lwjgl-glfw:${lwjglVersion}"
	compile "org.joml:joml:${jomlVersion}"
	compile "org.json:json:${jsonVersion}"
	compile "cz.advel.jbullet:jbullet:${jbulletVersion}"
	runtime "org.lwjgl:lwjgl:${lwjglVersion}:${lwjglNatives}"
	runtime "org.lwjgl:lwjgl-glfw:${lwjglVersion}:${lwjglNatives}"
	testCompile "junit:junit:${junitVersion}"
}

test {
	systemProperty "java.library.path", "native"
	jvmArgs "-XstartOnFirstThread"
}

task wrapper(type: Wrapper) {
	gradleVersion = '3.2'
}

shadowJar {
	baseName = 'radon-distribution'
	classifier = null
	version = null
}